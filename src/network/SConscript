#  -*- mode: python -*-

import os
from my_utils import mkdeps

Import(['env', 'testlibs', 'exports'])

stream_connection_deplibs = mkdeps([
    exports['base']['asio_dispatcher'],
    'boost_system',
    'boost_thread',
    ])
stream_connection = env.StaticLibrary(
    target='stream-connection',
    source=[
        'stream-connection.cc',
        ],
    LIBS=stream_connection_deplibs,
    )
stream_connection_pkg = [
    stream_connection,
    stream_connection_deplibs
    ]

tcp_connection_deplibs = mkdeps([
    exports['base']['asio_dispatcher'],
    stream_connection_pkg,
    'boost_system',
    'boost_thread',
    ])
tcp_connection = env.StaticLibrary(
    target='tcp-connection',
    source=[
        'tcp-connection.cc',
        ],
    LIBS=tcp_connection_deplibs,
    )
tcp_connection_pkg = [
    tcp_connection,
    tcp_connection_deplibs
    ]

ssl_connection_deplibs = mkdeps([
    exports['base']['asio_dispatcher'],
    stream_connection_pkg,
    'boost_system',
    'boost_thread',
    'crypto',
    'ssl',
    ])
ssl_connection = env.StaticLibrary(
    target='ssl-connection',
    source=[
        'ssl-connection.cc',
        ],
    LIBS=ssl_connection_deplibs,
    )
ssl_connection_pkg = [
    ssl_connection,
    ssl_connection_deplibs
    ]

http_connection_deplibs = mkdeps([
    exports['base']['asio_dispatcher'],
    exports['proto']['http_proto'],
    'boost_regex',
    'curl',
    ])
http_connection = env.StaticLibrary(
    target='http-connection',
    source=[
        'http-connection.cc',
        ],
    LIBS=http_connection_deplibs,
    )
http_connection_pkg = [
    http_connection,
    http_connection_deplibs
    ]

http_client_connection_deplibs = mkdeps([
    exports['proto']['http_proto'],
    http_connection_pkg,
    ssl_connection_pkg,
    tcp_connection_pkg,
    'boost_date_time',
    ])
http_client_connection = env.StaticLibrary(
    target='http-client-connection',
    source=[
        'http-client-connection.cc',
        ],
    LIBS=http_client_connection_deplibs,
    )
http_client_connection_pkg = [
    http_client_connection,
    http_client_connection_deplibs
    ]

http_server_connection_deplibs = mkdeps([
    exports['proto']['http_proto'],
    http_connection_pkg,
    stream_connection_pkg,
    ])
http_server_connection = env.StaticLibrary(
    target='http-server-connection',
    source=[
        'http-server-connection.cc',
        ],
    LIBS=http_server_connection_deplibs,
    )
http_server_connection_pkg = [
    http_server_connection,
    http_server_connection_deplibs
    ]

http_server_deplibs = mkdeps([
    exports['proto']['http_proto'],
    http_server_connection_pkg,
    tcp_connection_pkg,
    ])
http_server = env.StaticLibrary(
    target='http-server',
    source=[
        'http-server.cc',
        ],
    LIBS=http_server_deplibs,
    )
http_server_pkg = [
    http_server,
    http_server_deplibs
    ]

glacier_connection_deplibs = mkdeps([
    exports['proto']['http_proto'],
    exports['proto']['glacier_proto'],
    exports['base']['asio_dispatcher'],
    exports['util']['amazon_http_request_util'],
    http_client_connection_pkg,
    'crypto++',
    'json_spirit',
    ])
glacier_connection = env.StaticLibrary(
    target='glacier-connection',
    source=[
        'glacier-connection.cc',
        ],
    LIBS=glacier_connection_deplibs,
    )
glacier_connection_pkg = [
    glacier_connection,
    glacier_connection_deplibs
    ]

# Not all targets are exported; some are private.
network_exports = {
    'http_client_connection': http_client_connection_pkg,
    'http_server': http_server_pkg,
    'glacier_connection': glacier_connection_pkg,
}
Return('network_exports')

### Unit Tests

tcp_connection_test = env.Program(
    target='tcp-connection_test',
    source=[
        'tcp-connection_test.cc',
        ],
    LIBS=mkdeps([
        exports['base']['asio_dispatcher'],
        tcp_connection_pkg,
        testlibs,
        ]),
    )
run_tcp_connection_test = Alias(
    'run_tcp_connection_test',
    [tcp_connection_test],
    tcp_connection_test[0].path)
AlwaysBuild(run_tcp_connection_test)

http_client_connection_test = env.Program(
    target='http-client-connection_test',
    source=[
        'http-client-connection_test.cc',
        ],
    LIBS=mkdeps([
        exports['proto']['http_client_proto'],
        exports['base']['asio_dispatcher'],
        http_client_connection_pkg,
        testlibs,
        ]),
    )
run_http_client_connection_test = Alias(
    'run_http_client_connection_test',
    [http_client_connection_test],
    http_client_connection_test[0].path)
AlwaysBuild(run_http_client_connection_test)

glacier_connection_test = env.Program(
    target='glacier-connection_test',
    source=[
        'glacier-connection_test.cc',
        ],
    LIBS=mkdeps([
        exports['proto']['glacier_proto'],
        exports['base']['asio_dispatcher'],
        exports['services']['bundle_hasher'],
        glacier_connection_pkg,
        testlibs,
        'glacier_proto',
        ]),
    )
run_glacier_connection_test = Alias(
    'run_glacier_connection_test',
    [glacier_connection_test],
    glacier_connection_test[0].path)
AlwaysBuild(run_glacier_connection_test)

